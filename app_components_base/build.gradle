/**
 * apply configurations.
 */
apply from: "../configurations/armyknife_configure.groovy"
apply from: "../configurations/build_configure.groovy"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath('com.github.ben-manes:gradle-versions-plugin:0.20.0')
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: "com.github.ben-manes.versions"   // dependencies version check

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        aaptOptions.cruncherEnabled false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // for *.java options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // for Kotlin-Android
    androidExtensions {
        experimental = true
        features = ["parcelize"]
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/**_debug.kotlin_module'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true  // support robolectric
        }
    }
}

dependencies {
    implementation project(':app_factories')
    // Utils
    api('com.eaglesakura:light-saver:1.4.2') {
        because "Bundle save / restore"
        exclude group: 'com.google.code.gson'
    }
    api('com.eaglesakura:onactivityresult-invoke:1.3.1') {
        because "onActivityResult handler with Annotation."
    }
    ArmyKnifeLinker.apiToModule(project, 'kerberus')
    ArmyKnifeLinker.apiToModule(project, 'army-knife')
    ArmyKnifeLinker.apiToModule(project, 'army-knife-reactivex')
    ArmyKnifeLinker.apiToModule(project, 'firearm')
    ArmyKnifeLinker.apiToModule(project, 'firearm-channel')
    ArmyKnifeLinker.apiToModule(project, 'firearm-event')

    /**
     * Support Libraries
     * https://developer.android.com/topic/libraries/architecture/adding-components
     * https://developer.android.com/topic/libraries/support-library/refactor
     * https://developer.android.com/kotlin/ktx
     */
    api 'com.google.android.material:material:1.0.0'
    api('com.dmitrymalkovich.android:material-design-dimens:1.4') {
        because "Material design dimension(color, margin, or such else)"
    }

    ArmyKnifeLinker.allTestImplementationToModule(project, "army-knife-android-junit4")
    UnitTestDependenciesLinker.link(project)
}
